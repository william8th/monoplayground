plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.40"
}

group "com.williamheng"
version "1.0-SNAPSHOT"

subprojects {
    ext {
        kotlinVersion = "1.3.40"
        groovyVersion = "2.5.7"
        junitVersion = "4.12"
        jdbiVersion = "3.8.2"
        ktorVersion = "1.2.3"
        resilience4jVersion = "1.1.0"
        cucumberVersion = "4.7.1"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    ext.libs = [
            hikari     : [
                    "com.zaxxer:HikariCP:2.7.9"
            ],
            jdbi       : [
                    "org.jdbi:jdbi3-core:$jdbiVersion",
                    "org.jdbi:jdbi3-sqlobject:$jdbiVersion",
                    "org.jdbi:jdbi3-kotlin:$jdbiVersion"
            ],
            jdbiTesting: [
                    "org.jdbi:jdbi3-testing:$jdbiVersion",
                    "com.opentable.components:otj-pg-embedded:0.11.3"
            ],
            ktor       : [
                    "io.ktor:ktor-server-core:$ktorVersion",
                    "io.ktor:ktor-server-netty:$ktorVersion",
                    "io.ktor:ktor-jackson:$ktorVersion",
                    "io.ktor:ktor-metrics-micrometer:$ktorVersion"
            ],
            ktorClient : [
                    "io.ktor:ktor-client:$ktorVersion",
                    "io.ktor:ktor-client-jackson:$ktorVersion",
                    "io.ktor:ktor-client-apache:$ktorVersion"
            ],
            resilience4j: [
                    "io.github.resilience4j:resilience4j-retry:$resilience4jVersion",
                    "io.github.resilience4j:resilience4j-kotlin:$resilience4jVersion"
            ]
    ]

    apply plugin: "kotlin"
    apply plugin: "groovy"

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        implementation "org.codehaus.groovy:groovy:$groovyVersion"

        testImplementation "junit:junit:$junitVersion"
        testImplementation "org.hamcrest:hamcrest-all:1.3"

        runtime 'ch.qos.logback:logback-classic:1.2.3'
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    compileGroovy {
        dependsOn compileKotlin
        classpath += files(compileKotlin.destinationDir)
    }

}

